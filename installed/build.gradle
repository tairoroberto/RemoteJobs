apply plugin: 'com.android.application'
//apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.remotejobs.io.app"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }


    def keystoreProperties = new Properties()
    def keystorePropertiesFile = file('../local.properties')
    def env = System.getenv()

    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    } else {

        // Load keystore properties from environment or set their default values

        if (env['REMOTEJOBS_KEYSTORE_STORE']) keystoreProperties.put('REMOTEJOBS_KEYSTORE_STORE', env['REMOTEJOBS_KEYSTORE_STORE'])
        else keystoreProperties.put('REMOTEJOBS_KEYSTORE_STORE', "../remotejobs.jks")

        if (env['REMOTEJOBS_KEYSTORE_ALIAS']) keystoreProperties.put('REMOTEJOBS_KEYSTORE_ALIAS', env['REMOTEJOBS_KEYSTORE_ALIAS'])
        else keystoreProperties.put('REMOTEJOBS_KEYSTORE_ALIAS', "remotejobs")

        if (env['REMOTEJOBS_KEYSTORE_KEY_PASS']) keystoreProperties.put('REMOTEJOBS_KEYSTORE_KEY_PASS', env['REMOTEJOBS_KEYSTORE_KEY_PASS'])
        else keystoreProperties.put('REMOTEJOBS_KEYSTORE_KEY_PASS', "myDefaultStorePass")

        if (env['REMOTEJOBS_KEYSTORE_ALIAS_PASS']) keystoreProperties.put('REMOTEJOBS_KEYSTORE_ALIAS_PASS', env['REMOTEJOBS_KEYSTORE_ALIAS_PASS'])
        else keystoreProperties.put('REMOTEJOBS_KEYSTORE_ALIAS_PASS', "myDefaultKeyPass")
    }

    signingConfigs {
        debug {}
        release {
            storeFile file(keystoreProperties["REMOTEJOBS_KEYSTORE_STORE"])
            keyAlias keystoreProperties["REMOTEJOBS_KEYSTORE_ALIAS"]
            storePassword keystoreProperties["REMOTEJOBS_KEYSTORE_KEY_PASS"]
            keyPassword keystoreProperties["REMOTEJOBS_KEYSTORE_ALIAS_PASS"]
        }
    }

    buildTypes {
        debug {
            resValue "bool", "is_release", "false"
            minifyEnabled false
        }

        release {
            resValue "bool", "is_release", "true"
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation project(':base')
    implementation project(':splash')
    implementation project(':home')
    implementation project(':highestpaid')
    implementation project(':companies')
}

apply plugin: 'com.google.gms.google-services'